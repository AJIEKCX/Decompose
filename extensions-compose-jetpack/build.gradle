if (!isTargetEnabled(Target.ANDROID)) {
    return
}

plugins.apply('com.android.library')
plugins.apply('kotlin-android')
plugins.apply('maven-publish')

setupAndroid(project)
setupCompose(project)

android {
    lintOptions {
        disable "UnusedCrossfadeTargetStateParameter"
    }
}

dependencies {
    implementation project(':decompose')
    implementation "androidx.compose.foundation:foundation:$compose_jetpack_version"
    implementation "androidx.activity:activity-ktx:$androidx_activity_version"
}

afterEvaluate {
    publishing {
        publications {
            debug(MavenPublication) {
                from components.debug
                artifact sourceJar

                group = "com.arkivanov.decompose"
                artifactId = "extensions-compose-jetpack-debug"
                version = decompose_version

                setupPublicationPom(project, it)
            }

            release(MavenPublication) {
                from components.release
                artifact sourceJar

                group = "com.arkivanov.decompose"
                artifactId = "extensions-compose-jetpack"
                version = decompose_version

                setupPublicationPom(project, it)
            }
        }
    }

    setupPublicationRepository(project)
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}
