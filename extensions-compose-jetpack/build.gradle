plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

setupAndroid(project)
setupCompose(project)

dependencies {
    implementation project(':decompose')
    implementation "androidx.compose.foundation:foundation:$compose_jetpack_version"
}

afterEvaluate {
    publishing {
        publications {
            debug(MavenPublication) {
                from components.debug
                artifact sourceJar

                group = "com.arkivanov.decompose"
                artifactId = "extensions-compose-jetpack-debug"
                version = decompose_version

                setupPublicationPom(it)
            }

            release(MavenPublication) {
                from components.release
                artifact sourceJar

                group = "com.arkivanov.decompose"
                artifactId = "extensions-compose-jetpack"
                version = decompose_version

                setupPublicationPom(it)
            }
        }

        setupPublicationRepository(it)
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}
