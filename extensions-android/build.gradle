if (!isTargetEnabled(Target.ANDROID)) {
    return
}

plugins.apply('com.android.library')
plugins.apply('kotlin-android')
plugins.apply('maven-publish')

setupAndroid(project)

android {
    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=com.arkivanov.decompose.InternalDecomposeApi"
    }
}

dependencies {
    implementation project(':decompose')
}

afterEvaluate {
    publishing {
        publications {
            debug(MavenPublication) {
                from components.debug
                artifact sourceJar

                group = "com.arkivanov.decompose"
                artifactId = "extensions-android-debug"
                version = decomposeVersion

                setupPublicationPom(project, it)
            }

            release(MavenPublication) {
                from components.release
                artifact sourceJar

                group = "com.arkivanov.decompose"
                artifactId = "extensions-android"
                version = decomposeVersion

                setupPublicationPom(project, it)
            }
        }
    }

    setupPublicationRepository(project)
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}
